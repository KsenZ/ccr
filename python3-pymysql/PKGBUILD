# Maintainer: KsenZ <aksenzov@gmail.com>

pkgname=python3-pymysql
pkgver=0.7.4
pkgrel=1
pkgdesc="Pure Python MySQL Client"
arch=(any)
url="http://www.pymysql.org/"
license=('BSD')
depends=('python3')
makedepends=('python3-setuptools')
source=("https://files.pythonhosted.org/packages/source/P/PyMySQL/PyMySQL-$pkgver.tar.gz")
sha256sums=('5eb6f93373b3700e8b58abc5baa2fc23f50bbf21ad75e524747e583eea79b9e4')

build() {
    cd "$srcdir/PyMySQL-$pkgver"
    python3 setup.py build
}

package() {
    cd "$srcdir/PyMySQL-$pkgver"
    python3 setup.py install -O1 --skip-build --root="$pkgdir"
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

